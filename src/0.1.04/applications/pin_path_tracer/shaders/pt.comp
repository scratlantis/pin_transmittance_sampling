#include "interface_structs.glsl"
layout(binding = LOCAL_BINDING_OFFSET, FORMAT1) writeonly uniform image2D colorOut;
layout(binding = LOCAL_BINDING_OFFSET + 1) uniform VOLUME_INSTANCE { GLSLMediumInstance volInst;};

struct PushStruct
{
	float bitMaskPropability;
	uint bitMaskIterations;
};
layout(push_constant) uniform PC {PushStruct pc;};


#include "../lib/lib/pt_plot/generate_pt_state_histogram.glsl"
#include "../lib/lib/pt_smd_lib.glsl"

Range traceMediumConservative(Ray ray)
{
	vec3 localOrigin = (volInst.invMat * vec4(ray.origin, 1.0)).xyz;
	vec3 localDir = (volInst.invMat * vec4(ray.direction, 0.0)).xyz;
	vec3 localEntry, localExit;
	Range range;
	bool intersect = unitCubeIntersection(localOrigin, localDir, localEntry, localExit);
	if(intersect && distance(localEntry, localExit) > distance(localOrigin, localExit)) 
	{
		// Inside Cube
		vec3 worldExitPos = (volInst.mat * vec4(localExit, 1.0)).xyz;
		range.rMin = 0.0;
		range.rMax = distance(ray.origin, worldExitPos);
	}
	else if(intersect)
	{
	    // Intersect Cube
		vec3 worldEntryPos = (volInst.mat * vec4(localEntry, 1.0)).xyz;
		vec3 worldExitPos = (volInst.mat * vec4(localExit, 1.0)).xyz;
		range.rMin = distance(ray.origin, worldEntryPos);
		range.rMax = distance(ray.origin, worldExitPos);
	}
	else
	{
		// Miss Cube
		range.rMin = TMAX;
		range.rMax = TMAX;
	}
	return range;
}

float traceMedium(Ray ray, Range range, uint bounce, inout uint seed)
{
	vec3 localOrigin = (volInst.invMat * vec4(ray.origin + range.rMin * ray.direction, 1.0)).xyz;
	vec3 localExit = (volInst.invMat * vec4(ray.origin + range.rMax * ray.direction, 1.0)).xyz;
	vec3 localDir = normalize(volInst.invMat * vec4(ray.direction, 0.0)).xyz;
	float localTMax = distance(localOrigin, localExit);

	float tLocal = rayMarcheMedium(localOrigin, localDir, localTMax, seed);
	if(tLocal == TMAX)
	{
		return TMAX;
	}

	vec3 worldHitPos = (volInst.mat * vec4(localOrigin + tLocal * localDir, 1.0)).xyz;
	return distance(ray.origin, worldHitPos);
}

float traceMediumTransmittance(Ray ray, Range range, inout uint seed)
{
	vec3 localOrigin = (volInst.invMat * vec4(ray.origin + range.rMin * ray.direction, 1.0)).xyz;
	vec3 localExit = (volInst.invMat * vec4(ray.origin + range.rMax * ray.direction, 1.0)).xyz;
	vec3 localDir = normalize(volInst.invMat * vec4(ray.direction, 0.0)).xyz;
	float localTMax = distance(localOrigin, localExit);
	return rayMarcheMediumTransmittance(localOrigin, localDir, localTMax, seed);
}

float traceTransmittance(Ray ray, inout uint seed)
{
	float geomTransmittance = traceGeometryTransmittance(ray, 0x0F);
	if(geomTransmittance == 0.0) return geomTransmittance;

	Range rMedium = traceMediumConservative(ray);
	if(rMedium.rMin == TMAX) return geomTransmittance;

	return geomTransmittance * traceMediumTransmittance(ray, rMedium, seed);
}

#if AREA_LIGHT_COUNT > 0
void sampleDirectIllumAreaLight(vec3 pos, inout uint seed, out vec3 L_i, out vec3 omega_i)
{
	Ray ray = genDirectIllumRayAreaLight(pos, seed);
	float transmittance = traceTransmittance(ray, seed);
	L_i = transmittance * ray.weight;
	omega_i = ray.direction;
	
	SI_setTransmittance(transmittance)
	SI_setSourceDistance(ray.tmax)
}
#endif

void sampleDirectIllumEnvMap(vec3 pos, inout uint seed, out vec3 L_i, out vec3 omega_i)
{
	Ray ray = genDirectIllumRayEnvMap(pos, seed);
	float transmittance = traceTransmittance(ray, seed);
	L_i = transmittance * ray.weight;
	omega_i = ray.direction;

	SI_setTransmittance(transmittance)
	SI_setSourceDistance(ray.tmax)
}



Ray nextBounce(
	Ray ray, uint bounce, uint cullMask,
	inout uint seed,
	out uint hitType, out MaterialData matData, out mat4x3 tangentFrame)
{
	Ray nextRay;
	HitData hitData;
	float tGeom = traceGeometry(ray, cullMask, hitData);
	Range rMedium = traceMediumConservative(ray);
	rMedium.rMax = min(rMedium.rMax, tGeom);

	if(tGeom < rMedium.rMin)
	{
		hitType = PATH_VERTEX_TYPE_SURFACE_SCATTER;
		evalHit(hitData, matData, tangentFrame);
		nextRay = reflectLampertDiffuse(matData, tangentFrame, seed);
		nextRay.weight *= ray.weight;
	}
	else if( rMedium.rMin == TMAX )
	{
		hitType = PATH_VERTEX_TYPE_LEAF_INFINITY;
	}
	else
	{
		float tMedium = traceMedium(ray, rMedium, bounce, seed);
		if(tGeom < tMedium)
		{
			hitType = PATH_VERTEX_TYPE_SURFACE_SCATTER;
			evalHit(hitData, matData, tangentFrame);

			nextRay = reflectLampertDiffuse(matData, tangentFrame, seed);
			nextRay.weight *= ray.weight;
		}
		else if( tMedium == TMAX )
		{
			hitType = PATH_VERTEX_TYPE_LEAF_INFINITY;
		}
		else
		{
			hitType = PATH_VERTEX_TYPE_VOLUME_SCATTER;
			tangentFrame[3] = ray.origin + tMedium * ray.direction;
			matData.albedo = volInst.albedo;

			nextRay = scatterUniform(matData.albedo, tangentFrame[3], seed);
			nextRay.weight *= ray.weight;
		}
	}
	return nextRay;
}

void main()
{
	if(!validInvocation())
	{
		return;
	}
	uint seed = getSeed();
	vec3 totalIllumination = vec3(0.0);
	for(uint j = 0; j < SAMPLE_COUNT; j++)
	{
		SI_resetPts()
		Ray ray = genPrimaryRay(gl_GlobalInvocationID.xy, seed);
		SI_initPts(ray)
		for(uint i = 0; i < MAX_BOUNCES; i++)
		{
			uint hitType;
			MaterialData matData;
			mat4x3 tangentFrame;
			uint cullMask = 0xFF;

			Ray nextRay = nextBounce(ray, i, cullMask, seed, hitType, matData, tangentFrame);
			SI_nextBounce(nextRay, hitType)
			vec3 L_o = vec3(0.0);
			if(hitType == PATH_VERTEX_TYPE_LEAF)
			{
				totalIllumination += ray.weight * matData.emission;
				break;
			}
			else if(hitType == PATH_VERTEX_TYPE_LEAF_INFINITY)
			{
				if(i == 0)
				{
					totalIllumination += ray.weight * sampleEnvMap(ray.direction);
				}
				break;
			}
			else if(hitType == PATH_VERTEX_TYPE_SURFACE_SCATTER) 
			{
				if(i == 0) // Emission only from the first hit. Other emissions from light sources are collected via NEE
				{
					L_o += matData.emission;
				}
				vec3 brdf = lampertBRDF(matData.albedo);
				vec3 L_i, omega_i;
				float cosTerm;
				// Direct illumination from area lights
				#if AREA_LIGHT_COUNT > 0
				{
					SI_nextSample(RAY_DOMAIN_AREA_LIGHT)

					sampleDirectIllumAreaLight(tangentFrame[3], seed, L_i, omega_i);
					cosTerm = pos_dot(tangentFrame[2], omega_i);
					L_o += brdf * L_i * cosTerm;

					SI_setBSDF(brdf)
					SI_setCosineTerm(cosTerm)
					SI_setIncomingRadiance(L_i)
				}
				#endif
				// Direct illumination from envMap
				{
					SI_nextSample(RAY_DOMAIN_ENV_MAP)

					sampleDirectIllumEnvMap(tangentFrame[3], seed, L_i, omega_i);
					cosTerm = pos_dot(tangentFrame[2], omega_i);
					L_o += brdf * L_i * cosTerm;

					SI_setBSDF(brdf)
					SI_setCosineTerm(cosTerm)
					SI_setIncomingRadiance(L_i)
				}
			}
			else if(hitType == PATH_VERTEX_TYPE_VOLUME_SCATTER)
			{
				// Direct illumination from area lights
				vec3 bsdf = uniformScatterBSDF(volInst.albedo);
				vec3 L_i, omega_i;
				#if AREA_LIGHT_COUNT > 0
				{
					SI_nextSample(RAY_DOMAIN_AREA_LIGHT)

					sampleDirectIllumAreaLight(tangentFrame[3], seed, L_i, omega_i);
					L_o += bsdf * L_i;

					SI_setBSDF(bsdf)
					SI_setIncomingRadiance(L_i)
				}
				#endif
				// Direct illumination from envMap
				{
					SI_nextSample(RAY_DOMAIN_ENV_MAP)

					sampleDirectIllumEnvMap(tangentFrame[3], seed, L_i, omega_i);
					L_o += bsdf * L_i;

					SI_setBSDF(bsdf)
					SI_setIncomingRadiance(L_i)
				}
			}
			totalIllumination += ray.weight * L_o;
			ray = nextRay;
		}
		SI_finalizeSample()
	}

	totalIllumination /= float(SAMPLE_COUNT);
	imageStore(colorOut, ivec2(gl_GlobalInvocationID.xy), vec4(totalIllumination, 1.0));
	SI_setTotalContribution(totalIllumination)

	SI_initHist(200, 10000, 0.0, 1.0);

	uint bitmask = randomBitMask(pc.bitMaskPropability, pc.bitMaskIterations, seed);
	float bitMaskP = estimateBitPropability(bitmask);
	SI_setHistValue(bitMaskP, getFrameIdx());
	//SI_nextHist();


	//SI_writePtPlot()
}
